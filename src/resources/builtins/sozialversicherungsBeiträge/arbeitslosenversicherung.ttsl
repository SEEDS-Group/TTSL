package arbeitslosenversicherung

from gesetzlichePflegeversicherung import beitrag as gesetzlichePflegeVersicherungsBeitrag
# from gesetzlicheKrankenversicherung import beitrag as gesetzlicheKrankenVersicherungsBeitrag
from gesetzlicheRentenversicherung import beitrag as gesetzlicheRentenVersicherungsBeitrag

from gesetzlichePflegeversicherung import beitragArbeitgeber as gesetzlicherPflegeVersicherungsBeitragArbeitgeber
from gesetzlicheKrankenversicherung import beitragArbeitgeber as gesetzlicherKrankenVersicherungsBeitragArbeitgeber
from gesetzlicheRentenversicherung import beitragArbeitgeber as gesetzlicherRentenVersicherungsBeitragArbeitgeber

from einkommensgrenzen import geringfügigBeschäftigt
from einkommensgrenzen import inGleitzone
from einkommensgrenzen import midijobBemessungsentgeld
from einkommensgrenzen import midijobBeitragspflichtigeEinnahmeArbeitnehmer
from gesetzlicheRentenversicherung import beitragBruttolohn

from sozialversicherungsbeitrag import arbeitslosenversicherung as beitragssatzArbeitslosenversicherung

data per month bruttolohn: Float;

# TODO: Abhängig von Krankenversicherungsbeitrag, welcher abhängig vom Transfer Rente ist
# /**
#  * Sum of employee's social insurance contributions.
#  */
# public function per month sozialversicherungsBeitrag(): Float {
#     var pv = per month gesetzlichePflegeVersicherungsBeitrag();
#     var kv = per month gesetzlicheKrankenVersicherungsBeitrag();
#     var rv = per month gesetzlicheRentenVersicherungsBeitrag();
#     var av = per month beitrag();

#     return pv + kv + rv + av;
# }

# /**
#  * Sum of employer's and employee's social insurance contributions.
#  */
# private function  per month sozialversicherungsBeitragArbeitnehmerArbeitgeber(): Float {
#     return  per month sozialversicherungsBeitrag() +  per month sozialversicherungsBeitragArbeitgeber();
# }

/**
 * Sum of employer's social insurance contributions.
 */
public function  per month sozialversicherungsBeitragArbeitgeber(): Float{
    var pv =  per month gesetzlicherPflegeVersicherungsBeitragArbeitgeber();
    var kv =  per month gesetzlicherKrankenVersicherungsBeitragArbeitgeber();
    var rv =  per month gesetzlicherRentenVersicherungsBeitragArbeitgeber();
    var av =  per month beitragArbeitgeber();

    return pv + kv + rv + av;
}

/**
 * Employee's unemployment insurance contribution.
 */
public function  per month beitrag(): Float {
    to 2003-03-31 {
        if (geringfügigBeschäftigt()) {
            return 0;
        } else {
            return per month beitragBruttolohn() * beitragssatzArbeitslosenversicherung;
        }
    }

    from 2003-04-01 {
        if (geringfügigBeschäftigt()) {
            return 0;
        }

        if (inGleitzone()) {
            return per month beitragArbeitgeber();
        }

        return per month beitragBruttolohn() * beitragssatzArbeitslosenversicherung;
    }
}

/**
 * Employer's unemployment insurance contribution until March 2003.
 */
public function per month beitragArbeitgeber(): Float {
    to 2003-03-31 {
        if (geringfügigBeschäftigt()) {
            return 0;
        } else {
            return per month beitragBruttolohn() * beitragssatzArbeitslosenversicherung;
        }
    }

    from 2003-04-01 {
        if (geringfügigBeschäftigt()) {
            return 0;
        }

        if (inGleitzone()) {
            return per month midijobArbeitnehmer();
        }

        return per month beitragBruttolohn() * beitragssatzArbeitslosenversicherung;
    }
}

/**
 * Sum of employee's and employer's unemployment insurance contribution
 * for midijobs.
 */
private function per month midijobSummeArbeitgeberArbeitnehmer(): Float {
    from 2003-04-01 {
        return per month midijobBemessungsentgeld() * 2 * beitragssatzArbeitslosenversicherung;
    }
}

/**
 * Employers' unemployment insurance contribution for Midijobs until September
 * 2022.
 */
private function per month midijobArbeitgeberAnteilBruttolohn(): Float {
    from 2003-04-01 to 2022-09-30 {
        per month bruttolohn * beitragssatzArbeitslosenversicherung;
    }
}

/**
 * Employer's unemployment insurance contribution since October 2022.
 */
private function per month midijobArbeitgeber(): Float {
    from 2022-10-01 {
        return per month midijobSummeArbeitgeberArbeitnehmer() - per month midijobArbeitnehmer();
    }
}

/**
 * Employees' unemployment insurance contribution for Midijobs until September
 * 2022.
 */
private function per month midijobArbeitnehmer(): Float {
    from 2003-04-01 to 2022-09-30 {
        return per month midijobSummeArbeitgeberArbeitnehmer() - per month midijobArbeitgeber();
    }

    from 2022-10-01 {
        return midijobBeitragspflichtigeEinnahmeArbeitnehmer() * beitragssatzArbeitslosenversicherung;
    }
}
