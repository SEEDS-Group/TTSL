package arbeitslosenversicherung

from gesetzlichePflegeversicherung import beitrag as gesetzlichePflegeVersicherungsBeitrag
from gesetzlicheKrankenversicherung import beitrag as gesetzlicheKrankenVersicherungsBeitrag
from gesetzlicheRentenversicherung import beitrag as gesetzlicheRentenVersicherungsBeitrag

from gesetzlichePflegeversicherung import beitragArbeitgeber as gesetzlicherPflegeVersicherungsBeitragArbeitgeber
from gesetzlicheKrankenversicherung import beitragArbeitgeber as gesetzlicherKrankenVersicherungsBeitragArbeitgeber
from gesetzlicheRentenversicherung import beitragArbeitgeber as gesetzlicherRentenVersicherungsBeitragArbeitgeber

from einkommensgrenzen import geringfügigBeschäftigt
from einkommensgrenzen import inGleitzone
from einkommensgrenzen import midijobBemessungsentgeld
from einkommensgrenzen import midijobBeitragspflichtigeEinnahmeArbeitnehmer
from gesetzlicheRentenversicherung import beitragBruttolohn as gesetzlicheRentenversicherungBetragBruttolohn

from parameters.sozialversicherungsbeitrag import arbeitslosenversicherung as beitragssatzArbeitslosenversicherung

/**
 * Sum of employee's social insurance contributions.
 */
public function per month sozialversicherungsBeitrag(): Float {
    var pv = gesetzlichePflegeVersicherungsBeitrag() per month;
    var kv = gesetzlicheKrankenVersicherungsBeitrag() per month;
    var rv = gesetzlicheRentenVersicherungsBeitrag() per month;
    var av = beitrag() per month;

    return pv + kv + rv + av;
}

/**
 * Sum of employer's social insurance contributions.
 */
public function sozialversicherungsBeitragArbeitgeber(): Float per month {
    var pv = gesetzlicherPflegeVersicherungsBeitragArbeitgeber() per month;
    var kv = gesetzlicherKrankenVersicherungsBeitragArbeitgeber() per month;
    var rv = gesetzlicherRentenVersicherungsBeitragArbeitgeber() per month;
    var av = beitragArbeitgeber() per month;

    return pv + kv + rv + av;
}

/**
 * Sum of employer's and employee's social insurance contributions.
 */
private function sozialversicherungsBeitragArbeitnehmerArbeitgeber(): Float per month {
    return sozialversicherungsBeitrag() per month + sozialversicherungsBeitragArbeitgeber() per month;
}

/**
 * Employee's unemployment insurance contribution.
 */
public function beitrag(): Float per month {
    to 2003-03-31 {
        if (geringfügigBeschäftigt()) {
            return 0;
        } else {
            return gesetzlicheRentenversicherungBetragBruttolohn() per month * beitragssatzArbeitslosenversicherung;
        }
    }
    from 2003-04-01 {
        if (geringfügigBeschäftigt()) {
            return 0;
        }

        if (inGleitzone()) {
            return arbeitslosenversicherungBeitragArbeitnehmer() per month;
        }

        return gesetzlicheRentenversicherungBetragBruttolohn() per month * beitragssatzArbeitslosenversicherung;
    }
}

/**
 * Employer's unemployment insurance contribution until March 2003.
 */
public function beitragArbeitgeber(): Float per month {
    to 2003-03-31 {
        if (geringfügigBeschäftigt()) {
            return 0;
        } else {
            return gesetzlicheRentenversicherungBetragBruttolohn() per month * beitragssatzArbeitslosenversicherung;
        }
    }

    from 2003-04-01 {
        if (geringfügigBeschäftigt()) {
            return 0;
        }

        if (inGleitzone()) {
            return midijobArbeitnehmer() per month;
        }

        return gesetzlicheRentenversicherungBetragBruttolohn() per month * beitragssatzArbeitslosenversicherung
    }
}

/**
 * Sum of employee's and employer's unemployment insurance contribution
 * for midijobs.
 */
private function midijobSummeArbeitgeberArbeitnehmer(): Float per month {
    from 2003-04-01 {
        return midijobBemessungsentgeld() per month * 2 * beitragssatzArbeitslosenversicherung;
    }
}

/**
 * Employers' unemployment insurance contribution for Midijobs until September
 * 2022.
 */
private function midijobArbeitgeberAnteilBruttolohn(bruttolohn per month): Float per month {
    from 2003-04-01 to 2022-09-30 {
        bruttolohn per month * beitragssatzArbeitslosenversicherung;
    }
}

/**
 * Employer's unemployment insurance contribution since October 2022.
 */
private function midijobArbeitgeber(): Float per month {
    from 2022-10-01 {
        return midijobSummeArbeitgeberArbeitnehmer() per month - midijobArbeitnehmer() per month;
    }
}

/**
 * Employees' unemployment insurance contribution for Midijobs until September
 * 2022.
 */
private function midijobArbeitnehmer(): Float per month {
    from 2003-04-01 to 2022-09-30 {
        return midijobSummeArbeitgeberArbeitnehmer() per month - midijobArbeitgeber() per month;
    }

    from 2022-10-01 {
        return midijobBeitragspflichtigeEinnahmeArbeitnehmer() * beitragssatzArbeitslosenversicherung;
    }
}
